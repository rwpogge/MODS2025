#!/usr/bin/perl 
#
# mkMaskSnap - create a mask snapshot script
#
# Usage:
#   mkMaskSnap 
#
# Description:
#   Reads the $modsCfg/MODSn/slitmask.tab and creates a maskSnap.pro
#   script that will take thru-mask snapshots in dual imaging mode of
#   the MODS masks (those maskIDs prefixed with ID).  Use the SDSS g
#   filter in the blue and SDSS r in the red, with the
#   variable-intensity flat lamp.  parameters are set at the top of
#   this file.
#
# Author:
#   R. Pogge, OSU Astronomy Dept.
#   pogge@astronomy.ohio-state.edu
#   2012 Jun 10
#
# Modification History:
#   2015 May 19 - added logic below to make independent of the MODs
#                 instance (overlooked in MODS2 deployment) [rwp/osu]
#   2026 Jan 30 - MODS Archon controller update [rwp/osu]
#
#---------------------------------------------------------------------------

# Version and date

$verNum = "1.2.0";
$verDate= "2026-01-30";

# Full path to the MODS instrument configuration files, and the capacity
# of the slitmask cassette

$modsCfg = "/home/mods/Config"; 
$numMasks = 24;

# Who are we and where are we running?

my $hostID = `hostname`;
chomp($hostID);
my $modsID = uc($hostID);
my $userID = (getpwuid($<))[0];

# Standard modules we use

use Term::ANSIColor qw(:constants);  # color output 
$Term::ANSIColor::AUTORESET = 1;     # revert to normal after using color

# Mask imaging parameters

$vFlatLevel = 4.0;

$redExpTime = 1.0;
$redFilter  = "r_sdss";

$bluExpTime = 10.0;
$bluFilter  = "g_sdss";

# Apply focus offsets to blue and red channels for MOS mask images

$doFocOffset = 1;  # set to 0 to disable focus offset for masks
$redFocOff = 250; # microns from zero
$bluFocOff = 200;

# Process command-line arguments

$numArgs = $#ARGV + 1;
if ($numArgs > 1) {
    print "Usage: mkMaskSnap outFile\n";
    exit 1;
}

# Open slitmask table for the MODS unit we are on

if ($hostID eq "mods1") {
    $modsSlits = "$modsCfg/MODS1/slitmask.tab";
}
elsif ($hostID eq "mods2") {
    $modsSlits = "$modsCfg/MODS2/slitmask.tab";
}
else {
    print "You cannot run mkMaskSnap on host $hostID\n";
    exit 1;
}

open (MS,"<$modsSlits") || die "\nERROR: cannot open mask table $modsSlits: $!\n";
@mosID  = ();
$kount = 0;
while (<MS>) {
    $nextLine = $_;
    chomp($nextLine);
    $maskLine = simplify($nextLine);
    if (length($maskLine) > 0) { # skip blanks
	if (substr($maskLine,0,1) eq "#") {
	    # skip comments and lines filled with spaces
	}
	else {
	    ($maskNum,$maskID,$maskInfo) = split(' ',$nextLine,3);
	    $preFix = lc substr($maskID,0,2);
	    if ($preFix eq "id") {
		$mosID[$kount] = "$maskID";
		$kount++;
	    }
	}
    }
}
close(MS);
$numMOS = $kount;
if ($numMOS == 0) {
    print "No MOS masks found in $modsSlits - no maskSnap.pro script created\n";
    exit 1;
}

# Open and create the maskSnap.pro script

$outFile = "maskSnap_$hostID.pro";
open (MSS,">$outFile") || die "\nERROR: cannot create $outFile: $!\n";

# Write the script header

$dateNow = `date -u +"%Y %b %d - %T UTC"`;
chomp($dateNow);

print MSS "#\n";
print MSS "# Take thru-mask images of MOS Masks with the $modsID blue and red channels\n";
print MSS "#\n";
print MSS "# Number of Masks: $numMOS\n";
print MSS "# Generated by mkMaskSnap v$verNum [$verDate]\n";
print MSS "# Date: $dateNow\n";
print MSS "#\n";
print MSS "\n";
print MSS "CALMODE\n";
print MSS "\n";
print MSS "Archive:\n";
print MSS "  PARTNER CALIBRATION\n";
print MSS "  PROPID CALIBRATION\n";
print MSS "  PI_NAME ALL\n";
print MSS "  \n";
print MSS "Instrument:\n";
print MSS "  instconfig dual imaging\n";
if ($doFocOffset) {
    print MSS "  rcamfoc step $redFocOff\n";
    print MSS "  bcamfoc step $bluFocOff\n";
    print MSS "  imcslock\n";
}
print MSS "\n";
print MSS "Exec:\n";
print MSS "  print Make sure LBT elevation is locked at Zenith or El=60 at PA=0 before continuing.\n";
print MSS "  pause\n";
print MSS "  lamp vflat $vFlatLevel on\n";
print MSS "  red filter $redFilter\n";
print MSS "  red exptime $redExpTime\n";
print MSS "  blue filter $bluFilter\n";
print MSS "  blue exptime $bluExpTime\n\n";

for ($i=0;$i<$numMOS;$i++) {
    print MSS "$mosID[$i]:\n";
    print MSS "  slitmask $mosID[$i]\n";
    print MSS "  object $mosID[$i] Mask Image\n";
    print MSS "  go\n\n";
}

print MSS "# Clean up\n";
print MSS "\n  lamp off\n";
print MSS "\n";
print MSS "end\n";

close(MSS);

print "\nDone: created the $outFile script.\n";

exit;

#---------------------------------------------------------------------------
#
# simplify - remove leading and trailing whitespace from a string
#

sub simplify {
    my @out = @_;
    for (@out) {
	s/^\s+//;
	s/\s+$//;
    }
    return wantarray ? @out : $out[0];
}

