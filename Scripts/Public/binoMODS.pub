#!/bin/csh
#
# binoMODS - send response to both or either MODS in bino acq/exec
#
# Usage:
#   binoMODS response
# or
#   binoMODS [mods1|mods2] response
#
# A special command is
#   binoMOD abort
# which sends a Ctrl+C to both windows to hard abort both scripts.
#
# Overview:
#   acqBinoMODS and execBinoMODS run on each MODS and sometimes
#   ask for keyboard response.  This command can be issued from
#   the modsTerm tmux session and respond to both simultaneously
#   or one or other MODS script.
#
#   It is a convenience function that is more efficient than
#   clicking in the window, typing, then clicking the other. It
#   can also help keep execution more parallel.
#
#   Should be run from inside the modsTerm tmux session.
#
#   If you are not in a modsTerm, or none is running, it will give
#   instructions on what to do.  It resizes the terminal on launch to
#   a good size for the session, preset below.
#
# Author:
#   R. Pogge, OSU Astronomy Dept.
#   pogge.1@osu.edu
#   Original: 2026 Jan 26
#
# Modification History:
#   2026 Jan 27 - added "halt" directive to send Ctrl+C to one or both panes
#
#---------------------------------------------------------------------------

setenv modsBin /lbt/lbto/mods/bin
setenv configDir /lbt/lbto/mods/.config

set prog=`basename $0`

# process the command line

if ($#argv == 1) then
   set respStr = $argv[1]
   set whichMODS = 0
else if ($#argv == 2) then
   set whichMODS = $argv[1]
   set respStr = $argv[2]
else
   printf "\nUsage: binoMODS [1|2] str\n"
   printf "\nSends 'str' to MODS observing scripts in response to prompts\n"
   printf "during binocular MODS observing. Without 1 or 2, sends to both\n"
   printf "MODS scripts simultaneously.\n"
   printf "\nExamples:\n"
   printf "  binoMODS str\n"
   printf "    sends 'str' (no spaces) to both MODS panes in modsTerm\n"
   printf "  binoMODS 1 str\n"
   printf "    sends 'str' only to the MODS1 pane in modsTerm\n"
   printf "  binoMODS halt\n"
   printf "    send Ctrl+C to immediately halt execution of both scripts\n"
   printf "\nExamples of responses to prompts from acqMODS or execMODS:\n"
   printf "  enter\n"
   printf "  abort or X to respond to Abort\n"
   printf "  retry or R to respond to Retry\n"
   printf "  ignore or I to respond to Ignore\n"
   printf "\nA special directive:\n"
   printf "  binoMODS halt\n"
   printf "Will send both scripts Ctrl+C to immediately halt both MODS scripts.\n\n"
   exit 0
endif

# Do we have a modsTerm tmux session running?

tmux has-session -t modsTerm >& /dev/null
if ($status) then
   # There is no tmux session named modsTerm...
   printf "${prog} needs to be run in a modsTerm tmux session but none is running\n"
   printf "    Type 'modsTerm' to start a modsTerm tmux session and try again.\n"
   exit 1
else 
   # There IS a tmux session named modsTerm, but are we in it?
   if ( ! $?TMUX ) then
      # that would be a no...
      printf "${prog} needs to be run in a modsTerm tmux session, but you're not in it.\n"
      printf "   Either go to the active modsTerm tmux window on the desktop, or\n"
      printf "   type 'modsTerm' to attach to the current modsTerm session\n"
      printf "   and try again.\n"
      exit 1
   endif
endif

# Test special response strings

set testStr=`echo $respStr | tr '[:upper:]' '[:lower:]'`

# If "enter" or recognized aliases are respStr, we just sent C-m (respStr is cleared)

if ( $testStr == "enter" || $testStr == "ret" || $testStr == "return") then
   set respStr = ""
endif

# If "halt", sends Ctrl+C to immediately halt (abort) script execution

if ( $testStr == "halt" ) then
   set respStr = "C-c"
endif

#
# We are attached to the modsTerm tmux session.  Send respStr as-is to
# one or both MODS panes
#

if ( $whichMODS == 0 ) then
    tmux send-keys -t modsTerm:1.0 ${respStr} C-m
    tmux send-keys -t modsTerm:1.1 ${respStr} C-m
else if ( $whichMODS == 1 ) then
    tmux send-keys -t modsTerm:1.0 ${respStr} C-m
else
    tmux send-keys -t modsTerm:1.1 ${respStr} C-m
endif

exit 0
