#!/bin/csh -f
#
# start_modsccd - start the modsCCD agent safely
#
# usage: start_modsccd
#
# Verifies that we allowed to run the modsCCD agent and in the
# correct environment:
#   * we on a MODS Archon server (mods1blue, mods2red, etc.)
#   * are we an authorized user (dts or mods)?
#   * are we in a ccdTerm tmux shell?
#   * is the azcam server running?  modsCCD must run *after* azcam
#   * is modsCCD already running, and if so, by who
#   
# IMPORTANT: start_modsccd must only be run after start_azcam and verifying
#            that azcam is running.
# 
# R. Pogge (pogge.1@osu.edu)
# OSU Astronomy Department
# 2025 Oct 21 - original version
#
# Modification History:
#   2025 Dec 30 - less sloppy version
#
# See also start_azcam
#
#---------------------------------------------------------------------------

set prog=`basename $0`
set tmuxID="ccdTerm"
set modsBin="/home/dts/mods/bin"

# Are we on a MODS Archon server?

set myHost="${HOSTNAME}"

if ("$myHost" == "mods1blue" || "$myHost" == "mods1red" || "$myHost" == "mods2blue" || "$myHost" == "mods2red") then
   set modsID=`echo $myHost | cut -c 1-6`   # like mods1b, used by azcam
else
   printf "ERROR: you cannot run ${prog} on host ${myHost}.\n"
   printf "       Ask your ISA for assistance with this operation.\n"
   exit 1
endif

# Is this user allowed to run azcam?

set user=`echo $USER | tr '[:upper:]' '[:lower:]'`

if ("${user}" == "dts" || "${user}" == "mods") then
   set azcamUser=${user}
else
   printf "ERROR: User '${user}' is not permitted to run ${prog}.\n"
   printf "       Ask your ISA for assistance with this operation.\n"
   exit 1
endif

# Is the required tmux session running and are we in it?

tmux has-session -t ${tmuxID} >& /dev/null
if ($status) then
   # we are not in the right tmux session
   printf "${prog} needs to be run in a ${tmuxID} tmux session but none is running\n"
   printf "    Type '${tmuxID}' to start a ${tmuxID} tmux session and try again.\n"
   exit 1
else
   # There IS a tmux session named ${tmuxID}, but are we in it?
   if ( ! $?TMUX ) then
      # that would be a no...
      printf "${prog} needs to be run in a ${tmuxID} tmux session, but you're not in it.\n" 
      printf "   Either go to the active ${tmuxID} tmux window on the desktop, or\n"
      printf "   type '${tmuxID}' to attach to the current ${tmuxID} session\n"
      printf "   and try again.\n"
      exit 1
   endif
endif

# Is the azcam server running?  It has to be running before modsCCD starts

ps h -C ipython | grep azcam_mods >& /dev/null
if ($status) then
   printf "ERROR: The azcam server is not running.\n"
   printf "       You must run start_azcam before running ${prog}.\n"
   exit 1
endif

# Last check: is modsCCD already running?

ps h -C modsCCD >& /dev/null
if ($status) then
   # not running, start in the left pane of the ccdTerm
   printf "Starting the ${modsID} modsCCD agent...\n"
   tmux send-keys -t ${tmuxID}:1.0 "${modsBin}/modsCCD" C-m
else
   # it IS running, who is running it?
   set ccdUser=`ps eo user h -C modsCCD`
   printf "The ${modsID} modsCCD agent is already being run by user ${ccdUser}\n"
endif

exit 0
