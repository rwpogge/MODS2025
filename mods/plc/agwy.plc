/*!
  \mainpage agwy.plc - AGW Y PLC program

  \author Staff, OSU Dept. of Astronomy (rgonzale@astronomy.ohio-state.edu)
 
  \date 2006 Jul 20
  
  \section Usage
  
  Usage: Multi-level command set used with ISL system
	 or Window IMS system
  
  Where: \c MODS instrument
  
  \section Introduction
	Small command set the key words are -
	initial: Initialize AGW Y (move to CW or CCW and set pos=0.000)
	home: Send to position 0.000
	stow: Send to oposite of home.
	stop: Stop all mechanism movement
	subroutines:
		relbrk: Release break
		setbrk: Set break

	All Motion Parameters are documented in the MicroLYNX Part III 
	Software Reference.

<pre>
2005 Jul 20 - new application [rdg]
2008 Aug 15 - added Power Failure (PWRFAIL) variables [rdg]
</pre>

\todo

<ul>
</ul>
*/
/*!
  \file agwy.plc
  \brief Small command set for AGW Y mechanisms
*/
/** END HEADER **/
* START loading PLC
'Motion Configuration
MSEL=10
MUNIT=2000
MAC=50
MRC=34
MHC=0
HCDT=500
ACLT=1
ACCL=50		'acceleration rate in rev/sec^2
DECL=ACCL	'acceleration rate = deceleration rate
LDECL=500	'set decel after a limit is asserted
'
' Input & Output Configuration
IOS 21=13,0,0	'POSITIVE (CW) limit,active low,
IOS 22=14,0,0	'NEGATIVE (CCW)limit, active low
IOS 23=0,0,0	'SCIENCE position occupied sensor
IOS 26=18,1,0	'Brake relay coil (internal pull-up switch opened)
		'Output defaults to NO, Brake wired to NO contacts on relay
		'Brake is applied with power off and default state of
		'drive output

' User Variable Declarations
VAR VNORM=10.0   
VM=VNORM	'5 rev/sec running speed
VAR VSEEK=0.9
VAR YPOS 	'Y Stage position value
VAR PWRFAIL=1	'Power Failures of any kind
'
''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'	        STARTUP MODE
' PLC starts at address #100
'
PGM 100          'Starts at address 100
'
''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'		INITIAL(initializing) PROCEDURES
' INITIALize are identical
'
LBL INITIAL
LBL HOME
  PWRFAIL=0
  BR BITERR, IO 21+IO 22=2 'Check io bits 21 and 22 (both ON not allowed)
  BR SETPOS,IO 21=1
'
  VM=VNORM
  SLEW VNORM,0	'slew to to set position 0.000
   HOLD 2	'suspend procedure until move is complete
  POS=0.000	'set position to Zero (0.000) again with enough resolution
  VM=VSEEK	'set to sneaking velocity
  MOVA -0.5	'move to an absolute index of -0.500
   HOLD 2	'suspend procedure until move is complete
  SLEW VSEEK,0	'sneak up on limit switch.
   HOLD 2	'suspend procedure until move is complete
  VM=VNORM	'reset the speed to normal speed.
  POS=0.000	'set position to Zero (0.000) again with enough resolution
   DELAY 100
BR END_IT         'branch to exit

''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'		HOME(0.0 position) PROCEDURE
' HOME position is at the opposite end of HOME.  
'
'LBL HOME
'  BR PWRERR, PWRFAIL=1          'Check for Power Failure or was turned off/on
'  BR BITERR, IO 21+IO 22=2	'Check io bits 21 and 22 (both ON not allowed)
'  BR SETPOS,IO 21=1
'  VM=VNORM			'Set to default velocity
'  MOVA 0.0			'slew to to set position 0.000
'   DELAY 100
'BR END_IT
'
' Already in position
LBL SETPOS
  POS=0.0
BR END_IT

''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'		STOW(park) PROCEDURE
' STOW position is at the opposite end of HOME.  
'
LBL STOW
  VM=VNORM
  BR PWRERR, PWRFAIL=1		'Check for Power Failure or was turned off/on
  BR BITERR, IO 21+IO 22=2 	'Check io bits 21 and 22 (both ON not allowed)
  SLEW -VNORM,0		'slew to STOW position (opposite of HOME). 
   HOLD 2		'suspend procedure until move is complete
  VM=VSEEK		'set the speed for sneeking up.
  MOVA POS+0.500	'move to a relative index of 0.500
   HOLD 2		'suspend procedure until move is complete
  SLEW -VSEEK,0   	'sneak up on stow limit
   HOLD 2		'suspend procedure until move is complete
  VM=VNORM		'reset the speed to normal speed.
   DELAY 100
BR END_IT		'branch to exit

''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'		MOVETARG PROCEDURE
' MOVE Y Stage
'
LBL MOVETARG
  MOVA YPOS
'  HOLD 2
BR END_IT	'branch to exit

''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'		STOP(halt) PROCEDURE
' STOP all motion and end this function
'
LBL STOP
  SSTP 1 		'stop procedure
BR END_IT	'branch to exit

''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'		RELeaseBRaKe PROCEDURE
' Subroutine to release the brake.
'
LBL RELBRK
  IO 26=1
RET

''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'		SETBRaKe PROCEDURE
' Subroutine to set the brake.
'
LBL SETBRK
  IO 26=0
RET
'
''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'                BITERR(io error) PROCEDURE
' BITERR Both bits 21 and 22 "ON" not allowed.
'
LBL BITERR
  PRINT "? FAULT Sensor Fault, both limits asserted"
BR END_IT
'
''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'               PWRERR(power failure error) PROCEDURE
' I'm indicating a power failure
'
LBL PWRERR
  PRINT "? PWRFLR power failure, re-cycled or turned off (reset)"
BR END_IT
'
''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'		END_IT(exit) PROCEDURE
' EXIT the PLC routine
'
LBL END_IT       'Exit
END              'end the procedures
PGM              'end of program in RAM 

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'''''''''''''''''''' NEW PLC PGM STARTED '''''''''''''''''''''
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'		ISL PLC General PROCEDURES
'
PGM 6000

''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'                WHO AM I PROCEDURE
'
LBL WHO
  PRINT "agwy"
BR END_PLC

''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'                CHKPOS PROCEDURE check position
'
LBL CHKPOS
  PRINT POS
BR END_PLC       'branch to exit

''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'                CHKMVG PROCEDURE check moving status
'
LBL CHKMVG
  PRINT MVG
BR END_PLC       'branch to exit

''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'                POLL THE CONTROLLER PROCEDURE
'
LBL POLL
  PRINT "POS=",POS," : BITS26-21=",IO 26,IO 25,IO 24,IO 23,IO 22,IO 21
BR END_PLC       'branch to exit

''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'                CODE VERSION PROCEDURE
'
LBL CODEV
  PRINT "codeV=0.1(5/12/06)",":HW",VER
BR END_PLC      'branch to exit

''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'                IO20 PROCEDURE for bits 26 - 21
' Check IO 20 bits
'
LBL IO20
  PRINT IO 26,IO 25,IO 24,IO 23,IO 22,IO 21
BR END_PLC       'branch to exit

''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'                IO30 PROCEDURE for bits 36 - 31
' Check IO 30 bits
'
LBL IO30
  PRINT IO 36,IO 35,IO 34,IO 33,IO 32,IO 31
BR END_PLC       'branch to exit

''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'		UserIO20 PROCEDURE
' Subroutine to set IOS 20 for USER release of brake.
'
LBL UIO20
  PRINT "Change IO 20 back by typing HWIO20"
BR END_PLC

''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'		HardWareIO20 PROCEDURE
' Subroutine to reset IOS 20 normal by the hardware release of brake.
'
LBL HWIO20
  IOS 26=18,1,0	'Brake relay coil (internal pull-up switch opened)
		'Output defaults to NO, Brake wired to NO contacts on relay
		'Brake is applied with power off and default state of
		'drive output
BR END_IT
'
''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'                IO20MASK PROCEDURE
' Check the Select, Insert bit mask 24-21
'
LBL IO20MASK
  VAR B21=IO 21*8 
  VAR B22=IO 22*4 
  VAR B23=IO 23*2 
  VAR B24=IO 24
  VAR BITMASK=B21+B22+B23+B24
  PRINT BITMASK
BR END_PLC
'
''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'		END_PLC PROCEDURE
' END of ISL PLC code
'
LBL END_PLC		
END 
PGM


