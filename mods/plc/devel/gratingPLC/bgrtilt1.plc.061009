/*!
  \mainpage bgrtilt1.plc - bgrtilt1 PLC program
 
  \author Staff, OSU Dept. of Astronomy (rgonzale@astronomy.ohio-state.edu)
  
  \date 2007 Feb 02
   
  \section Usage
   
  Usage: Multi-level command set used with ISL system
         or Window IMS system
   
  Where: \c MODS instrument
   
  \section Introduction
        Small command set of key words are -
	Any item with a LBL is a keyword
		
        All Motion Parameters are documented in the MicroLYNX Part III
        Software Reference.

<pre>
2007 Feb 02 - new application header only [rdg]
2008 May 25 - added PLC code for the new application [rdg]
</pre>
\todo

<ul>
</ul>
*/
/*!
  \file bgrtilt1.plc
  \brief Small command set for BLUE grating tilt 1 mechanism
*/
/** END HEADER **/
' START loading PLC code
' Motion Parameters
DRVEN=1    'Disable Motor Until User Inputs New Target Grating
MSEL= 10   '10 microsteps per step
MUNIT=2000 'units to revs
MAC=50     'accel current is 0.50 * 7
MRC=30     'run current is 0.30 * 7
MHC=0      'hold current is zero
HCDT=0     'hold current delay msec
ACLT=1     'linear acceleration
'
'Input & Output Configuration
Ios 21 = 13,0,0 'CW Limit, Active Low
Ios 22 = 14,0,0 'CCW Limit, Active Low
'Ios 23 - 26 	'NOT used
' 
'Motion Configuration
Accl=50   	'Acceleration rate in rev/sec^2
Decl=Accl 	'Deceleration rate in rev/sec^2
Ldecl=500 	'Limit Deceleration in rev/sec^2
'
VAR TTIME=60000	'Wait for 30 secs before assuming anything.
VAR BKLIMIT	'position + compensation limit
'
VAR VNORM=25.0	'Move Velocity in rev/sec GRATING TILT parameter
VAR VSEEK=1.0	'Seeking speed.
VM=VNORM	'Set Speed to normal
'
'Variable Declarations
VAR TILT=0.0	'Grating tilt commanded position sent by host computer
'
' IEB Power detection
VAR PWRFAIL=1   	'Power Failures of any kind
VAR STEPCNT=-5.0	'User units stop and sneak up on limit
VAR RELAX=-1.75		'Negative offloading of tension. (optional?)
BLSH=10.0		'Amount of backlash. Settable by user
BLM=1			'1=Mechanical, 0=Mathematical (backlash)
BLE=1			'Backlash enable
VAR NVMPOS		'Position saved as fail safe
''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'               STARTUP MODE
'               PLC starts at address #100
'
PGM 100          'Starts at address #100
' 
''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'               INITIALize or POSition DATUM PROCEDURES
' Initialize or position datum grating tilt
LBL HOME
LBL INITIAL
  PWRFAIL=0			'Reset Power Failure
  VM=VNORM			'Reset Speed
  BR BITERR, IO 21+IO 22=2	'Both IO bits(21,22) ON not allowed
'
  TP1=STEPCNT,SCNT1	'Configure with user units
  TPE1=1               	'Enable user units
'
  TT2=TTIME, GRSUB2	'Configure time trip TTIME in millisecs.
  TTE2=1		'Enable trip one
'
  Drven=1		'Enable Drive
'
  SLEW VNORM,0		'slew to CW limit
  HOLD 2 		'suspend until completed
  POS=0.0		'set position to Zero
  Drven=0		'Enable Drive
BR END_IT
'
''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'	ZeroLimitTILT(zero position and tilt) PROCEDURE
' Initialize position by going to the CW limit and then
' go to the tilt setting.
LBL ZLTILTA
  '
  BR PWRERR, PWRFAIL=1		'Check for Power Failure
  BR BITERR, IO 21+IO 22=2	'Both IO bits(21,22) ON not allowed

  BLM=1			'1=Mechanical, 0=Mathematical (backlash)
  BLE=1
  Drven=1		'Enable Drive		
  MOVA TILT		'Move until position+backlash is reached
			'then do the backlash
  HOLD 2		'suspend until move and backlash have completed
  MOVR RELAX		'Release stored energy (relax the tension)
  HOLD 2		'suspend until move and backlash have completed
  Drven=0		'diable Drive
BR CHKSTA		'Check position status
'
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'	ZeroLimitTILT(zero position and tilt) PROCEDURE
' Initialize position by going to the CW limit and then
' go to the tilt setting.
LBL ZLTILTR
  '
  BR PWRERR, PWRFAIL=1		'Check for Power Failure
  BR BITERR, IO 21+IO 22=2	'Both IO bits(21,22) ON not allowed

  BLM=1			'1=Mechanical, 0=Mathematical (backlash)
  BLE=1
  Drven=1		'Enable Drive		
  MOVA TILT		'Move until position+backlash is reached
			'then do the backlash
  HOLD 2		'suspend procedure until move is complete
  MOVR RELAX            'Release stored energy (relax the tension)
  HOLD 2		'suspend procedure until move is complete
  Drven=0		'diable Drive
BR CHKSTA		'Check position status
'
''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'		GRatingSUBroutine2 PROCEDURE
' Timed out. We cannot go on forever.
LBL GRSUB2		'Turn off at user input revolutions
  PRINT "PLC TIME: ",TTIME," timed out"
RET
'  
''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'               SetpCouNT1 PROCEDURE
' Close or Open sneak reached
'
LBL SCNT1       'user units
  sstp 0
  SLEW VSEEK,0	'slow down to position
  HOLD 2
RET
'
''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'                BITERR(io error) PROCEDURE
' BITERR Both bits 21 and 22 "ON" not allowed.
LBL BITERR
  PRINT "1 BGRTILT1 Sensor Fault, both limits asserted, BGRTILT1=FAULT"
BR END_IT
'
''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'               PWRERR(power failure error) PROCEDURE
' I'm indicating a power failure
LBL PWRERR
  PRINT "1 BGRTILT1 power failure or turned off (reset), BGRTILT1=PWRFLR"
BR END_IT
'
''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'		CHecKSTAtus PROCEDURES
' Check where we are.
LBL CHKSTA
  BR GTHOME, POS=0.0
  PRINT "BGRTILT1=", POS
BR END_IT2
LBL GTHOME
  PRINT "BGRTILT1=HOME"
LBL END_IT2
BR END_IT
'
''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'		END_IT PROCEDURE
' END of PLC code
LBL END_IT
  NVMPOS=POS
END
PGM
'
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'''''''''''''''''''' NEW PLC PGM STARTED '''''''''''''''''''''
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'		ISL PLC General PROCEDURES
'
PGM 6000

''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'                WHO AM I PROCEDURE
'
LBL WHO
  PRINT "bgrtilt1"
  DELAY 100
BR END_PLC

''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'                CHKPOS PROCEDURE check position
'
LBL CHKPOS
  PRINT POS
  DELAY 100
BR END_PLC       'branch to exit

''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'                CHKMVG PROCEDURE check moving status
'
LBL CHKMVG
  PRINT MVG
  DELAY 100
BR END_PLC       'branch to exit

''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'                POLL THE CONTROLLER PROCEDURE
'
LBL POLL
  PRINT "POS=",POS," : BITS26-21=",IO 26,IO 25,IO 24,IO 23,IO 22,IO 21
  DELAY 100
BR END_PLC       'branch to exit

''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'                CODE VERSION PROCEDURE
'
LBL CODEV
  PRINT "codeV=0.1(06/12/07)",":HW",VER
  DELAY 100
BR END_PLC      'branch to exit

''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'                IO20 PROCEDURE for bits 26 - 21
' Check IO 20 bits
'
LBL IO20
  PRINT IO 26,IO 25,IO 24,IO 23,IO 22,IO 21
  DELAY 100
BR END_PLC       'branch to exit

''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'                IO30 PROCEDURE for bits 36 - 31
' Check IO 30 bits
'
LBL IO30
  PRINT IO 36,IO 35,IO 34,IO 33,IO 32,IO 31
  DELAY 100
BR END_PLC       'branch to exit
'
''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'                IO20MASK PROCEDURE
' Check the Select, Insert bit mask 24-21
'
LBL IO20MASK
  VAR B21=IO 21*8 
  VAR B22=IO 22*4 
  VAR B23=IO 23*2 
  VAR B24=IO 24
  VAR BITMASK=B21+B22+B23+B24
  PRINT "0 ",BITMASK
BR END_PLC
'
''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'		END_PLC PROCEDURE
' END of ISL PLC code
'
LBL END_PLC		
  DELAY 100
END 
PGM
