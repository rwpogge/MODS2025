/*!
  \mainpage agwFW.plc - AGW Filter Wheel PLC program

  \author Staff, OSU Dept. of Astronomy (rgonzale@astronomy.ohio-state.edu)
 
  \date 2006 Jul 20
  
  \section Usage
  
  Usage: Multi-level command set used with ISL system
	 or Window IMS system
  
  Where: \c MODS instrument
  
  \section Introduction
	Small command set the key words are -
	initial: Initialize AGW Filter Wheel (move to CW or CCW and set 
					     pos=0.000)
	home: Send to position 0.000
	stow: Send to oposite of home.
	stop: Stop all mechanism movement
	relbrk: Release break
	setbrk: Set break

	All Motion Parameters are documented in the MicroLYNX Part III 
	Software Reference.

<pre>
2006 Jul 20 - new application [rdg]
2006 Nov 01 - added home,initial,stow,stop,relbrk,setbrk [rdg]
</pre>

\todo

<ul>
</ul>
*/
/*!
  \file agwFW.plc
  \brief Small command set for AGW Filter Wheel mechanism
*/
/** END HEADER **/
* Start here with PLC code
*	'Motion Configuration
MSEL= 10
MUNIT=2000
MAC = 50
MRC=34
MHC = 0
HCDT = 500
ACLT = 1
ACCL=50		'acceleration rate in rev/sec^2
DECL=ACCL	'acceleration rate = deceleration rate
LDECL=500	'set decel after a limit is asserted
* Input & Output Configuration
IOS 21 = 13,0,0	'POSITIVE (CW) limit,active low,
IOS 22 = 14,0,0	'NEGATIVE (CCW)limit, active low
IOS 26=0	'Brake relay coil (internal pull-up switch opened)
*		'Output defaults to NO, Brake wired to NO contacts on relay
*		'Brake is applied with power off and default state of
*		'drive output
* Variable Declarations
VAR VNORM=10.0   
VM=VNORM	'5 rev/sec running speed
VAR VSEEK=0.5
*VAR BACKLASH = -->> 0.2? <<--
VAR WHO="agwFW"		'version of this PLC code
VAR CODEV="0.0"		'version of this PLC code

* MicroLYNX source code
PGM 100          'Starts at address 100
*
*			INITIAL(initializing) PROCEDURE
LBL INITIAL
CALL RELBRK
SLEW VNORM,0	'slew to to set position 0.000
HOLD 2		'suspend procedure until move is complete
POS=0.000	'set position to Zero (0.000) again with enough resolution
VM=VSEEK	'set the speed for sneeking up.
MOVA -0.5	'move to an absolute index of -0.500
HOLD 2		'suspend procedure until move is complete
SLEW VSEEK,0	'finetune the home position (position 0.000)
HOLD 2		'suspend procedure until move is complete
VM=VNORM	'reset the speed to normal speed.
POS=0.000	'set position to Zero (0.000) again with enough resolution
CALL SETBRK
BR END_IT         'branch to exit
*
*			STOW(park) PROCEDURE
LBL STOW
CALL RELBRK
SLEW -VNORM,0	'slew to STOW position (opposite of HOME). 
HOLD 2		'suspend procedure until move is complete
VM=VSEEK	'set the speed for sneeking up.
MOVA POS+0.500	'move to a relative index of 0.500
HOLD 2		'suspend procedure until move is complete
SLEW -VSEEK,0   'finetune the stow position
HOLD 2		'suspend procedure until move is complete
VM=VNORM	'reset the speed to normal speed.
BR END_IT	'branch to exit
*
*			HOME(Science Position) PROCEDURE
LBL HOME	
CALL RELBRK
MOVA -0.5	'move to an absolute index of -0.5
HOLD 2 		'suspend procedure until position change is complete
VM=VSEEK	'set the speed for sneeking up.
MOVA 0.000	'move to an absolute index of 0.000
HOLD 2		'suspend procedure until move is complete
VM=VNORM
BR END_IT	'branch to exit
*
*			STOP(halt) PROCEDURE
LBL STOP
SSTP 1 		'stop procedure
DELAY 500
BR END_IT	'branch to exit
*
*			RELeaseBReaK PROCEDURE
LBL RELBRK
IO 26=1
RET
*
*			SETBReaK PROCEDURE
LBL SETBRK
IO 26=0
RET 
*
*			END_IT(exit) PROCEDURE
LBL END_IT       'Exit
CALL SETBRK
END              'end the procedures
PGM              'end of program in RAM 
