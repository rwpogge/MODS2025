/*!
  \mainpage DarkSlide.plc - DarkSlide PLC program

  \author Staff, OSU Dept. of Astronomy (rgonzale@astronomy.ohio-state.edu)
 
  \date 2007 Feb 02
  
  \section Usage
  
  Usage: Multi-level command set used with ISL system
	 or Window IMS system
  
  Where: \c MODS instrument
  
  \section Introduction
	Small command set the key words are -
	initial: Initialize DarkSlide (move to CW or CCW and set 
					     pos=0.000)
	home: Send open to position 0.000
	stow: Send close position oposite of home.
	stop: Stop all mechanism movement
	relbrk: Release break
	setbrk: Set break

	All Motion Parameters are documented in the MicroLYNX Part III 
	Software Reference.

<pre>
2007 Feb 02 - new application [rdg]
</pre>

\todo

<ul>
</ul>
*/
/*!
  \file darkslide.plc
  \brief Small command set for darkslide mechanisms
*/
/** END HEADER **/
' Start here with PLC code
' Motion Configuration
MSEL= 10
MUNIT=2000
MAC = 50
MRC=40
MHC = 0
HCDT = 500
ACLT = 1
ACCL=50		'acceleration rate in rev/sec^2
DECL=ACCL	'acceleration rate = deceleration rate
LDECL=500	'set decel after a limit is asserted
'
' Input & Output Configuration
IOS 21 = 13,0,0	'POSITIVE (CW) limit,active low,
IOS 22 = 14,0,0	'NEGATIVE (CCW)limit, active low
'IOS 26=0	'Brake relay coil (internal pull-up switch opened)
		'Output defaults to NO, Brake wired to NO contacts on relay
		'Brake is applied with power off and default state of
		'drive output
' Variable Declarations
VAR VNORM=0.75   
VM=VNORM	'1 rev/sec running speed
VAR VSEEK=0.09
'VAR BACKLASH = -->> 0.2? <<--
VAR CMDER

''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'               'STARTUP MODE
' PLC starts at address #100
'
PGM 100		'Starts at address 100

''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'               INITIAL(initializing) and HOME PROCEDURES
' INITIALize mechanism
'
LBL INITIAL	'INITIAL(initializing) PROCEDURE
BR IOFAIL, IO 21=1
DELAY 100
SLEW VNORM,0	'slew to to set position 0.000
HOLD 2		'suspend procedure until move is complete
POS=0.000	'set position to Zero (0.000)
VM=VSEEK	'set the speed for sneeking up.
MOVA -0.05	'move to an absolute index of -0.05
HOLD 2		'suspend procedure until move is complete
SLEW VSEEK,0	'finetune the home position (position 0.000)
HOLD 2		'suspend procedure until move is complete
POS=0.000	'reset position to Zero (0.000).
BR END_IT       'branch to exit
'
''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'               OPEN(or home) PROCEDURE
'
LBL OPEN
BR IOFAIL, IO 21=1
DELAY 100
SLEW VNORM,0	'slew to to set position 0.000
HOLD 2		'suspend procedure until move is complete
BR END_IT       'branch to exit

''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'               CLOSE(close) PROCEDURE
'
LBL CLOSE
br IOFAIL, IO 22=1
DELAY 100
SLEW -VNORM,0	'slew to CLOSE position (opposite of OPEN). 
HOLD 2		'suspend procedure until move is complete
BR END_IT	'branch to exit

''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'		STOP(halt) PROCEDURE
'
LBL STOP
SSTP 1 		'stop procedure
BR END_IT	'branch to exit

''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'               RELeaseBRaKe PROCEDURE
' Subroutine to release the brake.
'
LBL RELBRK
IO 26=1
RET

''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'               IOFAIL PROCEDURE
' IO has failed or already there PROCEDURE
'
LBL IOFAIL
PRINT "Already in position ", POS," IO=",IO 21,IO 22
RET

''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'		SETBRaKe PROCEDURE
' Set the mechanism brake
LBL SETBRK
IO 26=0
RET 
 
''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'               BITERR(io error) PROCEDURE
' BITERR Both bits 21 and 22 "ON" not allowed.
'
LBL BITERR
PRINT "Bits 21(ON)=",IO 21," and 22(ON)=",IO 22," not allowed"
'reset iobits
IOBITS=0
DELAY 100
BR END_IT
 
''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'               END_IT(exit) PROCEDURE
' END of Main PLC code
'
LBL END_IT       'Exit
DELAY 100
END              'end the procedures
PGM              'end of program in RAM

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'''''''''''''''''''' NEW PLC PGM STARTED '''''''''''''''''''''
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'		ISL PLC General PROCEDURES
'
PGM 6000

''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'                WHO AM I PROCEDURE
'
LBL WHO
PRINT "hatch"
DELAY 100
BR END_PLC

''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'                CHKPOS PROCEDURE check position
'
LBL CHKPOS
PRINT POS
DELAY 100
BR END_PLC       'branch to exit

''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'                CHKMVG PROCEDURE check moving status
'
LBL CHKMVG
PRINT MVG
DELAY 100
BR END_PLC       'branch to exit

''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'                POLL THE CONTROLLER PROCEDURE
'
LBL POLL
PRINT "POS=",POS," : BITS26-21=",IO 26,IO 25,IO 24,IO 23,IO 22,IO 21
DELAY 100
BR END_PLC       'branch to exit

''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'                CODE VERSION PROCEDURE
'
LBL CODEV
PRINT "code ver = 0.1(latest)"
DELAY 100
BR END_PLC      'branch to exit

''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'                IO20 PROCEDURE for bits 26 - 21
' Check IO 20 bits
'
LBL IO20
PRINT IO 26,IO 25,IO 24,IO 23,IO 22,IO 21
DELAY 100
BR END_PLC       'branch to exit

''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'                IO30 PROCEDURE for bits 36 - 31
' Check IO 30 bits
'
LBL IO30
PRINT IO 36,IO 35,IO 34,IO 33,IO 32,IO 31
DELAY 100
BR END_PLC       'branch to exit

''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'		END_PLC PROCEDURE
' END of ISL PLC code
'
LBL END_PLC		
DELAY 100
END 
PGM
